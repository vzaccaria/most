/\.TH/d
s/\.SS/##/
s/\[/\\\[ /g
s/\]/\\\] /g
/\.SH NAME/ {
	N	
	s/\.SH/#/
	s/\n//
	s/NAME//
	s/\\-/\n## OVERVIEW\n/
}
/\.SH SYNOPSYS/ {
	s/\.SH/##/
	n	
	s/$/\`/
	s/^/\`/
	s/\\fB[ ]*\([^\} ]*\)[ ]*\\fP/\1/g
	s/\\fI[ ]*\([^\} ]*\)[ ]*\\fP/\1/g
}
/\.TP/ {
	s/\.TP/\n/	
	n	
	s/^/\* /
	s/$/. \n/
	s/\\fB[ ]*\([^\}]*\)[ ]*\\fP/\`\1\`/g
	s/\\fI[ ]*\([^\}]*\)[ ]*\\fP/\`\1\`/g
	n
	s/^/ /
	s/\\fB[ ]*\([^\}]*\)[ ]*\\fP/\`\1\`/g
	s/\\fI[ ]*\([^\}]*\)[ ]*\\fP/\`\1\`/g
}
/\.IP/ {
	s/\.IP/\n/	
	n	
	s/^/	\* /
	s/\\fB[ ]*\([^\}]*\)[ ]*\\fP/\`\1\`/g
	s/\\fI[ ]*\([^\}]*\)[ ]*\\fP/\`\1\`/g
}
s/\\fB[ ]*\([^\}]*\)[ ]*\\fP/\`\1\`/g
s/\\fI[ ]*\([^\}]*\)[ ]*\\fP/\`\1\`/g
s/\\f...\([^\]*\)\\fP/\`\1\`/g
s/\.SH/##/
